{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "0ccfd07d-ed13-4f77-b0c4-2713b1016e49",
   "metadata": {},
   "source": [
    "# Lemma{4ptiSuRetta2}"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "a9024a6c-de4b-4e10-a178-be9cfe7c88f0",
   "metadata": {},
   "outputs": [],
   "source": [
    "load(\"basic_functions.sage\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ac5b0ade-d1ca-4d2b-a672-43ed48c2c5dd",
   "metadata": {},
   "source": [
    "We can assume that $P_1 = (1: 0: 0)$. Then we define \n",
    "$P_2$, $P_3$ and $P_4$ such that are all collinear\n",
    "Note that $u_1$, $u_2$, $v_1$, $v_2$ can be assumed not zero, \n",
    "since we want distinct points."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 29,
   "id": "1dabe1ea-b5e2-4c95-b360-a42d7986a0c1",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = vector((1, 0, 0))\n",
    "P2 = vector(S, (A2, B2, C2))\n",
    "P3 = u1*P1+u2*P2\n",
    "P4 = v1*P1+v2*P2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a3ba03d8-8efe-4b7a-bb67-f069c40a9dc0",
   "metadata": {},
   "source": [
    "Construction of the matrix of the linear conditions:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 30,
   "id": "3372dd85-f759-492d-8894-a6bb3ed8a309",
   "metadata": {},
   "outputs": [],
   "source": [
    "M = condition_matrix([P1, P2, P3, P4], S, standard=\"all\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d93c15f3-f4cd-4959-b82e-a7da7515aa8d",
   "metadata": {},
   "source": [
    "In order to get the rank of M, we can erase the rows 0, 1, 2 and the \n",
    "columns 1 and 4 (with no conditions on the parameters) \n",
    "and we get a new matrix $MM$ which has rank $r$ iff M has \n",
    "rank $r+2$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 31,
   "id": "028fe0ca-b73d-4c83-bd93-d1e37dc7003f",
   "metadata": {},
   "outputs": [],
   "source": [
    "MM = M.matrix_from_rows_and_columns([3, 4, 5, 6, 7, 8, 9, 10, 11], \\\n",
    "                                    [0, 2, 3, 5, 6, 7, 8, 9])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "05c8c200-f19c-4a89-a12e-f62ebe043a92",
   "metadata": {},
   "source": [
    "MM has rank $\\leq 5$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 32,
   "id": "010114ab-52ed-40ba-9998-5175ffac2872",
   "metadata": {},
   "outputs": [],
   "source": [
    "J6 = S.ideal(MM.minors(6))\n",
    "assert(J6 == S.ideal(S(0)))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "05dea0fd-2255-4155-aac2-8e3f7341919a",
   "metadata": {},
   "source": [
    "Now we want to see when $MM$ has rank $< 5$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 33,
   "id": "4cfd25f1-dfd3-44df-aedb-1d92d3fbc003",
   "metadata": {},
   "outputs": [],
   "source": [
    "J5 = S.ideal(MM.minors(5))\n",
    "J5 = J5.saturation(v1*v2*u1*u2*(u2*v1-u1*v2))[0]\n",
    "J5r = J5.radical()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1627da71-e3cc-4e4d-b9bf-27d7be1b081b",
   "metadata": {},
   "source": [
    "$J5r$ has only one ideal which is the ideal generated by $\\sigma(P_1, P_2)$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 34,
   "id": "c691a51c-7138-4ad4-a685-898464cef972",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(J5r == S.ideal(sigma(P1, P2)))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "34c2756b-0375-452b-9fb2-bd5a56a9be2b",
   "metadata": {},
   "source": [
    "$MM$ cannot have rank $\\leq 4$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 35,
   "id": "8a52dbe4-e61e-42a4-9a23-1a57f277788f",
   "metadata": {},
   "outputs": [],
   "source": [
    "J4 = S.ideal(MM.minors(4))\n",
    "J4 = J4.saturation(v1*v2*u1*u2*(u2*v1-u1*v2))[0]\n",
    "J4 = J4.saturation(S.ideal(matrix([P1, P3]).minors(2)))[0]\n",
    "\n",
    "assert(J4 == S.ideal(S(1)))\n"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f8055a74-9671-4586-87d3-23f6922715c8",
   "metadata": {},
   "source": [
    "A further remark is that if $P_5$ is another point collinear with $P_1$, $P_2$, then the \n",
    "rank of the matrix $\\Phi(P_1, P_2, P_3, P_4, P_5)$ is again $\\leq 7$, therefore all the points of $t = P_1 \\vee P_2$ are eigenpoints for the cubics defined by $\\Lambda(M)$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 36,
   "id": "47d56165-4d19-43b7-9018-a45e58b7c4f5",
   "metadata": {},
   "outputs": [],
   "source": [
    "P5 = w1*P1+w2*P2\n",
    "M1 = condition_matrix([P1, P2, P3, P4, P5], S, standard=\"all\")\n",
    "MM1 = M1.matrix_from_rows_and_columns([3, 4, 5, 6, 7, 8, 9, 10, 11], \\\n",
    "                                    [0, 2, 3, 5, 6, 7, 8, 9])\n",
    "\n",
    "## the rank of MM1 is <= 5, so the rank of M1 is <= 7:\n",
    "JJ6 = S.ideal(MM1.minors(6))\n",
    "assert(JJ6 == S.ideal(S(0)))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b5c1a0b2-96d7-4fb1-b5c2-6f38f142eae2",
   "metadata": {},
   "source": [
    "Also $M_1$ has rank $\\leq 6$ iff $\\sigma(P_1, P_2) = 0$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 37,
   "id": "c939f043-cc3d-4998-b59e-cbde72446f5c",
   "metadata": {},
   "outputs": [],
   "source": [
    "JJ5 = S.ideal(MM1.minors(5))\n",
    "JJ5 = JJ5.saturation(v1*v2*u1*u2*(u2*v1-u1*v2))[0]\n",
    "JJ5r = JJ5.radical()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "43ff4794-9b4d-4994-92d4-15faee069eb3",
   "metadata": {},
   "source": [
    "JJ5r is the ideal generated by $\\sigma(P_1, P_2)$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 38,
   "id": "647b4ac8-7094-4714-b47a-0c4668907f84",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(JJ5r == S.ideal(sigma(P1, P2)))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bfad1139-be1f-42f3-9522-3842e1e85499",
   "metadata": {},
   "source": [
    "Here we can conclude that the matrix $M$ can have rank $6$ or $7$ and if $M$ has rank $6$, then the line $t = P_1 \\vee P_2$ is tangent to the isotropic conic and the line $t$ is a line of eigenpoints for the cubics obtained by $M$.\n",
    "\n",
    "Now we want to see that if a line $t$ is tangent to the isotropic conic in a point $P_1$ and if $P_1, P_2, P_3, P_4$ are four distinct points on $t$, then the rank of $\\Phi(P_1, \\dotsc, P_4)$ is $6$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 39,
   "id": "0c1f6669-13f2-4025-8822-1f317236a824",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = vector((1, ii, 0))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "c12f490f-f79a-4050-8a46-17eddac1fe44",
   "metadata": {},
   "source": [
    "The tangent line to the isotropic conic in $P_1$ is $x+iy$. We define 3 other points on it."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 40,
   "id": "cdd88411-53c5-4201-aff9-a5293880ee22",
   "metadata": {},
   "outputs": [],
   "source": [
    "tg = x+ii*y\n",
    "P2 = vector(S, (A2*ii, -A2, C2))\n",
    "P3 = u1*P1+u2*P2\n",
    "P4 = v1*P1+v2*P2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a11de59a-ca14-4a91-9011-982d4548c1eb",
   "metadata": {},
   "source": [
    "Then we define the condition martix and we verify that it has rank $6$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 42,
   "id": "2045c331-1967-4814-bf08-db423ff2df01",
   "metadata": {},
   "outputs": [],
   "source": [
    "M = condition_matrix([P1, P2, P3, P4], S, standard=\"all\")\n",
    "assert(M.rank() == 6)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.2",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.15"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}

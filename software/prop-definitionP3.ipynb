{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "932aa706-2bd1-4335-bbaa-2b372082f2ab",
   "metadata": {},
   "source": [
    "# Proposition prop:definitionP3"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7582fae6-8da5-4f50-9b28-235d32d3247b",
   "metadata": {},
   "source": [
    "We know that $\\delta_2 = 0$ in general gives that \n",
    "$$P3 = (s_{14} s_{15} s_{22} - s_{12}^2 s_{45}) P_1 + s_{12} (s_{11} s_{45} - s_{14} s_{15}) P_2$$\n",
    "hence, for some values of $s_{ij}$, the point $P_3$ is not defined.\n",
    "Here we want to see when this happens.\n",
    "The conclusion is that $P_3$ is always uniquely defined, unless:\n",
    "1) the points of the $V$-configuration are such that $s_{12}=0$, $s_{14}=0$\n",
    "   (or s13=0, s15=0, or...). In this case the unique cubic is a \n",
    "   cubic in which the eigenpoints are in configuration (5).\n",
    "2) the points of the $V$-configuration satisfy the condition \n",
    "   $s_{12} = 0$, $s_{23} = 0$. In this case, the line $P_1 \\vee P_2$ of the $V$-configuration\n",
    "   is tangent in $P_2$ to the isotropic conic, so the matrix $\\Phi(P_1, P_2, P_3)$ has \n",
    "   rank $5$ and $P_5$ can be chosen in a free way on the line $P_1 \\vee P_4$ and the \n",
    "   matrix $\\Phi(P_1, P_2, P_3, P_4, P_5)$ has rank $9$, (unless $P_1 \\vee P_4$ is tangent\n",
    "   to the isotropic conic...)\n",
    "3) The points satisfy the conditions $\\sigma(P_1, P_2) = 0$ and \n",
    "   $\\sigma(P_1, P_4) = 0$. In this case the $V$-configuration is given \n",
    "   by a point $P_1$ not on the isotropic conic and the other points on the two tangents \n",
    "   to the isotropic conic through $P_1$. If no points of the $V$-configurations are on \n",
    "   the isotropic conic, the two tangent lines are lines of eigenpoints, if (say)\n",
    "   $P_2$ and $P_4$ are on the isotropic conic, the matrix $\\Phi(P_1, P_2, P_3, P_4)$ has rank $8$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "ef6af78b-3f2c-4a72-9cb9-10a97b64ea31",
   "metadata": {},
   "outputs": [],
   "source": [
    "load(\"basic_functions.sage\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "5bb6a540-1664-4278-9154-b462cb2a3456",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = vector(S, (A1, B1, C1))\n",
    "P2 = vector(S, (A2, B2, C2))\n",
    "P4 = vector(S, (A4, B4, C4))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "643c0ab0-5b4e-452f-862f-e165a96ec3bb",
   "metadata": {},
   "outputs": [],
   "source": [
    "## delta2 in terms of sij\n",
    "d2s = s12*s13*s45-s14*s15*s23  "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "dfa20ff4-67bd-4526-a7f2-2dabc9ae8179",
   "metadata": {},
   "outputs": [],
   "source": [
    "## In general, we have that 5 points in a V-configuration are eigenpoints \n",
    "## iff P3 = (s14*s15*s22-s12^2*s45)*P1+s12*(s11*s45-s14*s15)*P2, \n",
    "## unless the two coefficients of P1 and P2 are 0. Here we study this \n",
    "## particular case.\n",
    "## \n",
    "## Hence:\n",
    "Js = S.ideal(s14*s15*s22-s12^2*s45, s11*s12*s45-s12*s14*s15, d2s)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "60849a9b-1224-4b8d-871a-969182f5d73f",
   "metadata": {},
   "outputs": [],
   "source": [
    "## We study the ideal Js:\n",
    "pd = Js.radical().primary_decomposition()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "2b17e7d2-627f-415b-bdba-deb841e996c3",
   "metadata": {},
   "outputs": [],
   "source": [
    "## we get 6 ideals:\n",
    "assert(len(pd)==6)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "25507b2a-06a6-49fe-8840-db8665232694",
   "metadata": {},
   "outputs": [],
   "source": [
    "## the first ideal is generated by s14 and s12:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "d61836da-7402-4982-9a39-46a358666dd5",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(pd[0] == S.ideal(s12, s14))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "17c75f88-ddce-4f59-a67a-321d7a67146b",
   "metadata": {},
   "outputs": [],
   "source": [
    "## we construct 5 points in a V configuration such that \n",
    "## s12 = 0, s14 = 0:\n",
    "P1 = vector(S, list(wedge_product(P2, P4)))\n",
    "P3 = u1*P1+u2*P2\n",
    "P5 = v1*P1+v2*P4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "7854f54d-aa29-4eb2-b6fe-10f8227cff2d",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(scalar_product(P1, P2) == 0)\n",
    "assert(scalar_product(P1, P4) == 0)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "9852f6ac-ecea-4798-81a5-ea5d9bbae44a",
   "metadata": {},
   "outputs": [],
   "source": [
    "## we have: delta2(P1, P2, P3, P4, P5) = 0:\n",
    "assert(delta2(P1, P2, P3, P4, P5) == 0)\n",
    "## we see therefore that we can define P3 and P5 in an arbitrary way:\n",
    "## for any choice delta2 is zero.\n",
    "\n",
    "## from here the computations are exactly the computations in the file \n",
    "## config5.sage (up to a permutation of the points) and we see that in \n",
    "## this case the eigenpoints of the cubic are in configuration (5) of the \n",
    "## figure of the possible configurations.\n",
    "\n",
    "#### CONCLUSION FOR THE CASE s12=0, s14=0:\n",
    "## If s12=0, s14=0, then the V-configuration has delta2 = 0 and p3 and P5 \n",
    "## are free (on the line P1+P2 and P1+P4 respectrively). "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "91d2e2fe-0730-45a5-bacd-84b4d38aea16",
   "metadata": {},
   "outputs": [],
   "source": [
    "######################## \n",
    "## the ideal pd[1] is s12, s22, s13:\n",
    "\n",
    "assert(pd[1] == S.ideal(s12, s22, s23))\n",
    "\n",
    "## an easy computation shows that if s22 = 0 and s12 = 0, then s23 = 0, \n",
    "## (or if s12 = 0, s23 = 0, then s22 = 0)\n",
    "## so the ideal is generated by s12 and s22. In particular, the line \n",
    "## P1+P2 is tangent to the isotropic conic in P2, hence the matrix \n",
    "## Phi(P1, P2, P3) has rank 5, therefore, for all P3 and P5 the rank \n",
    "## of the matrix Phi(P1, P2, P3, P4, P5) is <= 9, therefore we have a \n",
    "## $V$ configuration which satisfies delta2(P1, P2, P3, P4, P5) = 0 \n",
    "## for any choice of the points P3 and P5.\n",
    "###\n",
    "### CONCLUSION FOR THE CASE s12, s22:\n",
    "### the V config is of eigenpoints, with every choice of P3 and P5\n",
    "### (if also P1+P4 is tangent to Ciso, the two tangent lines are \n",
    "### lines of eigenpoints, as claimed in the section on eigenpoints \n",
    "### of dim > 0.\n",
    "###### here is an example:\n",
    "## It shows that in case s12=0 and s22 = 0 we have 7 eigenpoints\n",
    "## with (apparently) no particular specific properties.\n",
    "\n",
    "# Q1, Q2 = vector(S, (0, 0, 1)), vector(S, (1, ii, 0))\n",
    "# Q3 = vector(S, (1, -ii, 0))\n",
    "# rt1 = det(matrix([Q1, Q2, (x, y, z)]))\n",
    "# rt2 = det(matrix([Q1, Q3, (x, y, z)]))\n",
    "# \n",
    "# p1 = 1*Q1\n",
    "# p2 = 1*Q2\n",
    "# p3 = 2*p1+5*p2\n",
    "# p4 = vector(S, (3, 1, 5))\n",
    "# p5 = p1+3*p4\n",
    "# assert(matrixEigenpoints([p1, p2, p3, p4, p5]).rank() == 9)\n",
    "# cb = cubic_from_matrix(matrixEigenpoints([p1, p2, p3, p4, p5]))\n",
    "# eigenpoints(cb)\n",
    "# assert(cb.is_prime())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "b5018cee-7086-485e-8079-63b7f81a0d92",
   "metadata": {},
   "outputs": [],
   "source": [
    "#######################\n",
    "## The ideal pd[2] is S.ideal(s15, s12):\n",
    "\n",
    "assert(pd[2] == S.ideal(s15, s12))\n",
    "##\n",
    "## this case is analog to the case s12, s14 (4<-->5)\n",
    "##"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "d35106bf-6471-4810-9900-9f2bbe3dcf9d",
   "metadata": {},
   "outputs": [],
   "source": [
    "#######################\n",
    "## ==> The ideal pd[3] is S.ideal(s45, s14):\n",
    "assert(pd[3] == S.ideal(s45, s14))\n",
    "\n",
    "## In this case we have s44 = 0, indeed:\n",
    "## (P4|v1*P1+v2*P4) = 0\n",
    "## v1*(P1|P4) + v2*(P4|P4) = 0\n",
    "## 0 + v2*(P4|P4) = 0\n",
    "## (P4|P4) = 0\n",
    "\n",
    "## CONCLUSION: THIS CASE IS THE SAME AS THE CASE pd[1]\n",
    "\n",
    "## "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "8d4285a6-417a-4226-9e3d-02f6efca669c",
   "metadata": {},
   "outputs": [],
   "source": [
    "########################\n",
    "## ==> The ideal pd[4] is S.ideal(s45, s15):\n",
    "assert(pd[4] == S.ideal(s45, s15))\n",
    "\n",
    "## In this case we have s55 = 0\n",
    "\n",
    "## CONCLUSION: THIS CASE IS THE SAME AS THE CASE pd[1]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 25,
   "id": "654d42e0-66ef-4c73-9634-a1434c69c9fb",
   "metadata": {},
   "outputs": [],
   "source": [
    "########################\n",
    "## ==> The ideal pd[5] is:\n",
    "## (s13*s22 - s12*s23, s14*s15 - s11*s45, s12*s13 - s11*s23, s12^2 - s11*s22)\n",
    "\n",
    "assert(pd[5] == S.ideal((s13*s22 - s12*s23, s14*s15 - s11*s45, \\\n",
    "                         s12*s13 - s11*s23, s12^2 - s11*s22)))\n",
    "\n",
    "## In this case we determine the ideal in the variables A1, ..., C4:\n",
    "## but first we have to re-define the points:\n",
    "P1 = vector(S, (A1, B1, C1))\n",
    "P2 = vector(S, (A2, B2, C2))\n",
    "P4 = vector(S, (A4, B4, C4))\n",
    "P3 = u1*P1+u2*P2\n",
    "P5 = v1*P1+v2*P4\n",
    "\n",
    "J = pd[5].subs({s11: scalar_product(P1, P1), s22: scalar_product(P2, P2), \\\n",
    "                s13: scalar_product(P1, P3), s12: scalar_product(P1, P2), \\\n",
    "                s23: scalar_product(P2, P3), s14: scalar_product(P1, P4), \\\n",
    "                s15: scalar_product(P1, P5), s45: scalar_product(P4, P5)}) \n",
    "\n",
    "## here Jrel can help:\n",
    "Jrel = S.ideal(s11-scalar_product(P1, P1), \\\n",
    "               s12-scalar_product(P1, P2), \\\n",
    "               s22-scalar_product(P2, P2), \\\n",
    "               s14-scalar_product(P1, P4), \\\n",
    "               s24-scalar_product(P2, P4), \\\n",
    "               s44-scalar_product(P4, P4), \\\n",
    "               s13-scalar_product(P1, P3), \\\n",
    "               s23-scalar_product(P2, P3), \\\n",
    "               s34-scalar_product(P3, P4), \\\n",
    "               s33-scalar_product(P3, P3))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 26,
   "id": "ab0ec935-c75e-4c15-a4ac-12efd2df5c31",
   "metadata": {},
   "outputs": [],
   "source": [
    "## We saturate J w.r.t. the condition that v2 != 0 and \n",
    "## w.r.t. the condition that P1, P2, P4 are not aligned.\n",
    "\n",
    "J = J.saturation(v2)[0]\n",
    "J = J.saturation(det(matrix([P1, P2, P4])))[0]\n",
    "\n",
    "## We study the ideal J\n",
    "\n",
    "## J contains the two polynomials sigma(P1, P2) and sigma(P1, P4):\n",
    "\n",
    "assert(sigma(P1, P2) in J)\n",
    "assert(sigma(P1, P4) in J)\n",
    "\n",
    "## moreover, J and the ideal (sigma(P1, P2), sigma(P1, P4))\n",
    "## coincide if P1, P2, P4 are not aligned:\n",
    "assert(J == S.ideal(sigma(P1, P2), \\\n",
    "            sigma(P1, P4)).saturation(det(matrix([P1, P2, P4])))[0])\n",
    "\n",
    "## hence in this case the V  configuration is such that \n",
    "## is obtained by a point P1 and the two lines are the two tangents to \n",
    "## Ciso passing through P1. In general, we get that the V-config is \n",
    "## such that the two lines are lines of eigenpoints and the cubic splits \n",
    "## into the conic (x^2 + y^2 + 2/3*z^2) and a line. This case is discussed\n",
    "## in the section on eigenpoints of positive dimension.\n",
    "#### Here is an example:\n",
    "#### example of P1+P2 tangent to Ciso in P2:\n",
    "\n",
    "# Q1, Q2, Q3 = vector(S, (0, 0, 1)), vector(S, (1, ii, 0)), vector(S, (1, -ii, 0))\n",
    "# rt1 = det(matrix([Q1, Q2, (x, y, z)]))\n",
    "# rt2 = det(matrix([Q1, Q3, (x, y, z)]))\n",
    "# \n",
    "# p1 = 1*Q1\n",
    "# p2 = Q1+3*Q2\n",
    "# p3 = 2*Q1+5*Q2\n",
    "# p4 = Q1+7*Q3\n",
    "# p5 = Q1-2*Q3\n",
    "# assert(matrixEigenpoints([p1, p2, p3, p4, p5]).rank() == 9)\n",
    "# cb = cubic_from_matrix(matrixEigenpoints([p1, p2, p3, p4, p5]))\n",
    "# cb.factor()  ## is z*(x^2 + y^2 + 2/3*z^2)\n",
    "\n",
    "##### In the particular case in which p2 and p4 are on Ciso, we \n",
    "## get that the matrix Phi(P1, P2, P3, P4, P5) has rank 8 and \n",
    "## a generic cubic of the corresponding pencil of cubics is smooth\n",
    "## and has 7 eigenpoints. The case is studied in the section on \n",
    "## V conf of rank 8.\n",
    "\n",
    "## Here is an example:\n",
    "# Q1, Q2, Q3 = vector(S, (0, 0, 1)), vector(S, (1, ii, 0)), vector(S, (1, -ii, 0))\n",
    "# rt1 = det(matrix([Q1, Q2, (x, y, z)]))\n",
    "# rt2 = det(matrix([Q1, Q3, (x, y, z)]))\n",
    "\n",
    "# p1 = 1*Q1\n",
    "# p2 = 1*Q2\n",
    "# p3 = 2*Q1+5*Q2\n",
    "# p4 = 1*Q3\n",
    "# p5 = Q1-2*Q3\n",
    "# assert(matrixEigenpoints([p1, p2, p3, p4, p5]).rank() == 8)\n",
    "# M1 = matrixEigenpoints([p1, p2, p3, p4, p5])\n",
    "# M2 = M1.matrix_from_rows([1, 2, 3, 4, 7, 9, 10, 13])\n",
    "# assert(M2.rank() == 8)\n",
    "# M3 = M2.stack(vector(S, (1, 3, 6, -4, 0, 3, 2, 1, -5, 7)))\n",
    "# M3 = M3.stack(vector(S, mon))\n",
    "# cb = M3.det()\n",
    "# eigenpoints(cb)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4c2fa341-b46f-4042-9fac-ca94f5b45a01",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.2",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.9.15"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}

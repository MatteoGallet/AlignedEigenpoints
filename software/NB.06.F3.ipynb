{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "b91cf53b-0c1a-4a07-8ee1-3a6d88a5da7e",
   "metadata": {},
   "source": [
    "# Proposition"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9df79333-dee9-4af4-ae50-7f7e614ce9ab",
   "metadata": {},
   "source": [
    "Let $t$ be a line of $\\mathbb{P}^2$.\n",
    "* If $t$ is not tangent to the isotropic conic, $t \\subseteq \\mathrm{Eig}(C)$ for a cubic $C$ if and only $C = t^2\\ell$, where $\\ell$ is any line of the plane.\n",
    "* If $t$ is tangent to the isotropic conic (in a point~$P$), $t \\subseteq \\mathrm{Eig}(C)$ for a cubic $C$ if and only if\n",
    "$$\n",
    "    C = t^2 \\ell+\\lambda C(r_0),\n",
    "$$\n",
    "where $\\ell$ is any line of the plane, $\\lambda \\in \\mathbb{C}$,  $r_0$ is any fixed line passing through $P$ different from $t$ and $C(r_0)$ is defined by\n",
    "$$\n",
    "C(r) = \\bigl( r^2-3\\left(a^2+b^2+c^2\\right) \\mathcal{Q}{\\mathbb{iso}} \\bigr) \\, r \\,\n",
    "$$\n",
    "Moreover, if $t$ is tangent to the isotropic conic at $P$, any cubic $C$ with $t \\subseteq \\mathrm{Eig}(C)$ is singular in $P$ and $t$ is one of the tangents $C$ in $P$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 1,
   "id": "c6194204-6b9a-436e-b1fa-c30589a5a410",
   "metadata": {},
   "outputs": [],
   "source": [
    "load(\"basic_functions.sage\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4521a4d8-6a0d-4259-b8c5-b50b06032659",
   "metadata": {},
   "source": [
    "## If a cubic $C$ has an eigenline $t$ tangent to the isotropic conic, then the equation of $C$ is $F_1$, so we have a linear system of cubics of dimension 3.\n",
    "## This linear system is of the form $t^2\\ell+\\lambda C(r)$ for any fixed line $r$ different from $t$. Hence the family of cubics $F_1$ and the family of cubics $t^2\\ell+\\lambda C(r)$ coincide."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "e55c6f8b-33a5-4496-89f1-f5ff7ccd8e66",
   "metadata": {},
   "source": [
    "We define the point $P_1$ and the line tg1, tangent to Ciso in $P_1$, the eigenpoint locus of the generic cubic F of the plane"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 2,
   "id": "62cfa9fc-ad9f-4f01-ad71-3d90069d7737",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = vector((1, ii, 0))\n",
    "tg1 = x+ii*y\n",
    "eigF = eig(F)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4886044a-7211-4561-9d0c-52931e1a41af",
   "metadata": {},
   "source": [
    "In order to have that the points of tg1 are eigenpoints, we substitute $x=-iy$ into eigF and we get a list of three equations that \n",
    "have to be satisfied for all values of $x$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 3,
   "id": "4acc10ba-77b5-41af-8052-60636a9b9ff1",
   "metadata": {},
   "outputs": [],
   "source": [
    "EE = list(eigF.subs(x = -ii*y)) "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "63365c54-aff8-4543-bbf7-43d3c089b138",
   "metadata": {},
   "source": [
    "We construct the list eqs of equations in $a_0, \\dotsc, a_9$ that must be satisfied in order to have tg1 eigenline:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 4,
   "id": "bf34eed1-de80-42c2-9fdb-92399372073b",
   "metadata": {},
   "outputs": [],
   "source": [
    "eqs = [ee.coefficient(mm) for mm in mon for ee in EE]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "244e421c-96c8-41fb-a0cf-de2aed6f1086",
   "metadata": {},
   "source": [
    "In order to obtain the cubics $F_1$ which have tg1 as eigenline, we reduce $F$ w.r.t.\\ the ideal $(eqs)$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 5,
   "id": "a1aee098-a955-4db0-ae5b-4a0f494d450e",
   "metadata": {},
   "outputs": [],
   "source": [
    "F1 = S.ideal(eqs).reduce(F)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bb826c1a-e211-4fc3-a915-2152bba9bf35",
   "metadata": {},
   "source": [
    "$F_1$ has the variables $x, y, z, a_2, a_3, a_6, a_9$, hence is a linear space of dimension 3 in $\\mathbb{P}^9$ generated by four cubics which are \n",
    "the polynomials $G_1, G_2, G_3, G_4$ below:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 6,
   "id": "4ac4f9e6-b6f5-46ea-82bd-6a75f9244460",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(F1.variables() == (x, y, z, a2, a3, a6, a9))\n",
    "\n",
    "G1, G2, G3, G4 = (\n",
    "    F1.coefficient(a2), F1.coefficient(a3),\n",
    "    F1.coefficient(a6), F1.coefficient(a9)\n",
    ")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b883b173-3cb9-4396-ad38-0795cd6363c4",
   "metadata": {},
   "source": [
    "We define four other cubics which are $H_1$, $H_2$, $H_3$ as follows:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 7,
   "id": "8f5756fd-9be9-4e0f-a2f7-64df510894ba",
   "metadata": {},
   "outputs": [],
   "source": [
    "H1, H2, H3 = x*tg1^2, y*tg1^2, z*tg1^2"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "42fef6bf-e5a3-4054-8f15-52c201d2c19a",
   "metadata": {},
   "source": [
    "and $H_4$, constructed in this way:\n",
    "we take the generic line $r$ passing through the point $P_1$ , (which is $l_1tg1+l_2z$)\n",
    "and $H_4$ is the corresponding cubic whose eigenpoints are the\n",
    "two tangent lines to Ciso in the points $r \\cap Ciso$, which is given by $C(r)$, i.e.\n",
    "the formula $(r^2-3(a^2+b^2+c^2)Ciso)r$ (where $a=l_1, b= il_1, c=il_2$)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 8,
   "id": "bf281016-e0f2-45c1-93ee-bde80b517deb",
   "metadata": {},
   "outputs": [],
   "source": [
    "r = l1*tg1+l2*z\n",
    "H4 = r*(r^2-3*(r.coefficient(x)^2+r.coefficient(y)^2+r.coefficient(z)^2)*Ciso)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "de17516a-3769-4b16-8afc-ca147ad759db",
   "metadata": {},
   "source": [
    "We verify that $H_1, H_2, H_3, H_4$ are 4 linearly independent cubics, i.e. we see that the rank of the matrix $N_1$ below is always 4 (unless $l_2 = 0$, i.e. unless tg1 = r)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 9,
   "id": "65a53e4a-6ac9-4196-9a08-65ab74454b3c",
   "metadata": {},
   "outputs": [],
   "source": [
    "N1 = matrix([[hh.coefficient(mn) for mn in mon] for hh in [H1, H2, H3, H4]])\n",
    "JN1 = S.ideal(N1.minors(4))\n",
    "assert(JN1.radical() == S.ideal(l2)) ## N1 has always rank 4"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "14f276b5-b510-4624-97f5-0c46f0770429",
   "metadata": {},
   "source": [
    "We verify that the linear space generated by $G_1, G_2, G_3, G_4$\n",
    "coincides with the linear space generated by $H_1, H_2, H_3, H_4$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 10,
   "id": "5f78a5f8-86d2-437b-90d8-974640879010",
   "metadata": {},
   "outputs": [],
   "source": [
    "for hh in [H1, H2, H3, H4]:\n",
    "    assert(\n",
    "        matrix(\n",
    "            [\n",
    "                [gg.coefficient(mn) for mn in mon] for gg in [G1, G2, G3, G4] + [hh.coefficient(mn) for mn in mon]\n",
    "            ]\n",
    "        ).rank() == 4\n",
    "    )\n",
    "\n",
    "for gg in [G1, G2, G3, G4]:\n",
    "    assert(\n",
    "        matrix(\n",
    "            [\n",
    "                [hh.coefficient(mn) for mn in mon] for hh in [H1, H2, H3, H4] + [gg.coefficient(mn) for mn in mon]\n",
    "            ]\n",
    "        ).rank() == 4\n",
    "    )"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "3db954b7-37ef-491b-8229-37e9cdb60281",
   "metadata": {},
   "source": [
    "## All the cubics $F_1$ are singular in $P_1$ and that tg1 is tangent to $F_1$ in $P_1$.\n",
    "## We assume $a_9$ not zero, since if $a_9=0$, $F_1$ is reducible of the form $t^2\\ell$, where $t$ is the line tg1"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 11,
   "id": "d1b8af4e-1b58-4026-994a-99df53030a8a",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(F1.subs(a9=0).quo_rem((x+ii*y)^2)[1] == 0)  ## (x+ii*y)^2 is a factor of the cubic\n",
    "assert(gdn(F1).subs(substitution(P1)) == vector(S, (0,0,0)))  ## P1 is singular\n",
    "assert(S.ideal(F1, tg1).saturation(a9)[0] == S.ideal(tg1, z^3))  ## P1 is a triple point"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fc2feaae-2517-46dd-ad3b-ec60ba717cdd",
   "metadata": {},
   "source": [
    "In order to describe $F_1$, we first determine when $F_1$ splits into a line and a conic. The line necessarily passes through $P_1$, hence is the line $r$ above. A generic point of $r$ is $(u_1l_2, u_2l_2, -l_1(u_1+iu_2)$, hence we define the point $p_3$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 12,
   "id": "af24926d-d118-403e-9161-c08a4fda0f0b",
   "metadata": {},
   "outputs": [],
   "source": [
    "p3 = vector(S, (u1*l2, u2*l2, -l1*(u1+ii*u2)))\n",
    "assert(r.subs(substitution(p3)) == 0)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "45024b92-033f-4d1c-a611-fa1962304437",
   "metadata": {},
   "source": [
    "Now we impose that, for all $u_1$ and $u_2$, the point $p_3$ is a zero of $F_1$. With this substitution $F_1$ splits into $(u_1+iu_2)^2$ and another factor, linear in $u_1$ and $u_2$ and we want that for all $u_1$ and $u_2$ this factor is zero:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 13,
   "id": "6a70918a-8568-4ace-aac9-0e6cca98bd75",
   "metadata": {},
   "outputs": [],
   "source": [
    "ff1 = F1.subs(substitution(p3))\n",
    "assert(ff1.quo_rem((u1+ii*u2)^2)[1] == 0)\n",
    "ff2 = ff1.quo_rem((u1+ii*u2)^2)[0]\n",
    "assert(ff2.degree(u1) ==1)\n",
    "assert(ff2.degree(u2) == 1)\n",
    "jf1 = S.ideal(ff2.coefficient(u1), ff2.coefficient(u2))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "b2df7840-f3a9-4ed2-8c76-97036c095ae9",
   "metadata": {},
   "source": [
    "The ideal jf1 is the ideal of the conditions on the parameters of $F_1$ \n",
    "(which are $a_2, a_3, a_6, a_9$) in order to have that $V(F_1)$ splits \n",
    "into the line $r$ and a conic. We can saturate it w.r.t.\\ $a_9$ and $l_2$. We get an ideal \n",
    "jf2 which is generated by the three polynomials $h_1, h_2, h_3$ below:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 14,
   "id": "c467b24e-6b36-4514-a4c4-3c28c70f3070",
   "metadata": {},
   "outputs": [],
   "source": [
    "jf2 = jf1.saturation(a9*l2)[0]\n",
    "h1 = l2*a2 + (3*ii)*l2*a3 + 3*l1*a9\n",
    "h2 = l1*a2^2 + (6*ii)*l1*a2*a3 - 9*l1*a3^2 + (-9*ii)*l2*a3*a9 - 9*l1*a6*a9\n",
    "h3 = a2^3 + (9*ii)*a2^2*a3 - 27*a2*a3^2 + (-27*ii)*a3^3 - 9*a2*a6*a9 + (-27*ii)*a3*a6*a9 + (27*ii)*a3*a9^2\n",
    "assert(S.ideal(h1, h2, h3).saturation(a9)[0] == jf2)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "111309ee-6e46-426d-8ee5-da4cd35e6a0d",
   "metadata": {},
   "source": [
    "The meaning of the above computation is the following: \n",
    "$V(F_1)$ contains a line $r$ iff $a_2, a_3, a_6, a_9$ satisfy the condition $h_3=0$. \n",
    "(The values of $l_1$ and $l_2$ which determine the line $r$ can be computed from the linear system obtained from $h_1$ and $h_2$). Hence:\n",
    "\n",
    "$F_1$ splits into the product of a linear factor and a quadratic factor iff $h_3(a_2, a_3, a_6, a_9) = 0$.\n",
    "\n",
    "Now a similar computation: we want to see when a line passing thorugh $P_1$ is tangent to the cubic $V(F_1)$ in $P_1$. \n",
    "Again, the line is the line $r$ above, we consider the ideal generated by $F_1$ and $r$ and we study it. We compute its primary decomposition\n",
    "and we get two ideals: the radical of the first is the point $P_1$, the second is generated by two poynomials $g_1$ and $g_2$ linear in \n",
    "$x, y, z$ (up to an admissible saturation) ($g_1$ is $r$) and $g_1, g_2$ give therefore a further point of intersectrion of $r$ with the cubic $V(F_1)$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 15,
   "id": "534e81bf-0404-4bbf-b532-5b7ecbe24626",
   "metadata": {},
   "outputs": [],
   "source": [
    "J1 = S.ideal(F1, r)\n",
    "pd = J1.primary_decomposition()\n",
    "assert(len(pd) == 2)\n",
    "assert(pd[0].radical() == S.ideal(x+ii*y, z))\n",
    "g1 = pd[1].gens()[0]\n",
    "g2 = pd[1].gens()[1]\n",
    "assert(g1 == r)\n",
    "assert(g1.degree(x) == 1)\n",
    "assert(g1.degree(y) == 1)\n",
    "assert(g1.degree(z) == 1)\n",
    "assert(g2.degree(x) == 1)\n",
    "assert(g2.degree(y) == 1)\n",
    "assert(g2.degree(z) == 1)\n",
    "assert(pd[1] == S.ideal(g1, g2).saturation(l2)[0])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "819cb85a-744a-4dde-a4e4-39c45fcd4cab",
   "metadata": {},
   "source": [
    "At this point we know that the third intersection of the line $r$ with $V(F_1)$ is given by the linear system $g_1=0, g_2=0$. \n",
    "We want that this point (for suitable values of $l_1$ and $l_2$) is again the point $P_1$. In this way we determine when $r$ is \n",
    "tangent to $V(F_1)$. Since $g_1$ is $r$, it always contain $P_1$. The only condition that has to be verified is that \n",
    "$P_1$ annihilates $g_2$. This condition is $f = l_2a_2+3il_2a_3+3l_1a_9 = 0$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 16,
   "id": "c23c7ca5-f589-4e91-959c-41e6043a8b20",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(g1.subs(substitution(P1)) == 0)\n",
    "f = l2*a2 + (3*ii)*l2*a3 + 3*l1*a9\n",
    "assert(g2.subs(substitution(P1)) == l2 * f)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "67792b36-4f9f-409d-b8a5-87e7f008dddd",
   "metadata": {},
   "source": [
    "Hence we obtain that $r$ is tangent in $P_1$ to $V(F_1)$ when $l_1, l_2$ are such that $f=0$. The tangent is $r_2$ (we do not get tg1, because we exclude the case $l_2=0$):"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 17,
   "id": "ae5f0229-b490-44a1-a3b6-0c25db37182d",
   "metadata": {},
   "outputs": [],
   "source": [
    "r2 = r.subs({l1:f.coefficient(l2), l2: -f.coefficient(l1)})"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "329267bb-1250-47a4-8f87-23ee3ed4def2",
   "metadata": {},
   "source": [
    "In general, the line $r_2$ is the second tangent in $P_1$ to the cubic. To verify this, we study the ideal generated by $F_1$ and $r_2$. Its primary decomposition (after saturation w.r.t. $a_9$) has two components, the first is the triple point $P_1$, the second contains the polynomial $h_3$, which means that it appears only when $F_1$ is reducible."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 18,
   "id": "3390dba0-da3d-470a-a508-e05374a9985d",
   "metadata": {},
   "outputs": [],
   "source": [
    "pd2 = S.ideal(F1, r2).saturation(a9)[0].primary_decomposition()\n",
    "assert(len(pd2) == 2)\n",
    "assert(z^3 in pd2[0]) \n",
    "assert(pd2[0].radical() == S.ideal(x+ii*y, z))  ## pd2[0] is the triple point $P_1$.\n",
    "assert(h3 in pd2[1])"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "064698b1-b2f6-4d57-9b79-7baa710c82da",
   "metadata": {},
   "source": [
    "Hence, when $F_1$ is irreducible, $V(F_1)$ has two tangents in $P_1$, which are: tg1 and r2. We want to see when they are coincident, i.e. when $P_1$ is a cusp. Here we see that this happens iff $a_9=0$, i.e. iff the cubic is splits into $t^2 \\ell$, where $t$ is the line tg1. Therefore:\n",
    "\n",
    "If $V(F_1)$ is irreducible, it has two distinct tangents tg1 and $r_2$, so the point $P_1$ is never a cusp for the cubic."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 19,
   "id": "b94dd7fb-96d1-4c8c-94c1-c5e2a9c9fe3b",
   "metadata": {},
   "outputs": [],
   "source": [
    "coinc_l = S.ideal(matrix([[tg1.coefficient(vr) for vr in [x, y, z]], [r2.coefficient(vr) for vr in [x, y, z]]]).minors(2))\n",
    "assert(coinc_l.groebner_basis() == [a9])\n",
    "assert(F1.subs(a9=0).quo_rem((x+ii*y)^2)[1] == 0)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "99e591d8-c7ee-43a8-ab97-db638579db48",
   "metadata": {},
   "source": [
    "When $V(F_1)$ is reducible, i.e. when $h_3=0$, $F_1$ splits into the product of $r_2$ and a conic. We can easily verify this, since $h_3$ is linear in $a_6$, so the equation $h_3=0$ can be solved w.r.t. $a_6$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 20,
   "id": "7b9a5c8d-2a98-408a-a9f4-390f195d0abe",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(h3.degree(a6) == 1)\n",
    "st = {a6: -h3.subs(a6=0)/h3.coefficient(a6)}\n",
    "F2 = S(numerator(F1.subs(st)))\n",
    "assert(F2.quo_rem(r2)[1] == 0)"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.1",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}

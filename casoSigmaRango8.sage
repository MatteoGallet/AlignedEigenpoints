#### here we study the case
## sigma(1,2) = 0, sigma(1, 4) = 0
## i.e. line P1 + P2 and line P1 + P4 tangent to the isotropic conic.
## We will prove that the matrix Phi(P1, P2, P3, P4, P5) cannot 
## have rank < 9.

varAn1 = ["s"+str(i)+str(j) for i in range(1,8) for j in range(i, 8)]
varAn2 = ["x", "y", "z", "u1", "u2", "v1", "v2", "w1", "w2", "l1", "l2"]
varAn3 = ["a"+str(i) for i in range(10)]
varAn4 = [str(XX)+str(i) for i in range(1, 8) for XX in ["A", "B", "C"]]
var("xx")
K = QQ
K.<ii> = NumberField(xx^2+1)
S = PolynomialRing(K, varAn1+varAn2+varAn3+varAn4)
S.inject_variables(verbose=false)

P1 = vector(S, (1, 0, 0))
Q2 = vector(S, (0, ii, 1))
Q4 = vector(S, (0, -ii, 1))
P2 = u1*P1 + u2*Q2
P4 = v1*P1 + v2*Q4
P3 = w1*P1 + w2*Q2
P5 = l1*P1 + l2*Q4

load("auxiliaryProcedures.sage")

mon = [x^3, x^2*y, x*y^2, y^3, x^2*z, x*y*z, y^2*z, x*z^2, y*z^2, z^3]

### In this configuration, the line P1+P2 is tangent to
### the isotropic conic in Q2 and the line P1+P4 is tangent
### to the isotropic conic in Q4 (for all P3 and P5)

M = matrixEigenpoints([P1, P2, P3, P4, P5])


## Here we see that the rows 2, 5, 8, 11, 14 of M can be erased
## since P1[0], P2[0], P3[0], P4[0] and P5[0] are invertible
assert(Set([P1[0], P2[0], P3[0], P4[0], P5[0]]) == Set([1, l1, w1, u1, v1]))
## and we have:
## P1[2]*M[0]-P1[1]*M[1]+P1[0]*M[2] = 0
## P2[2]*M[3]-P2[1]*M[4]+P2[0]*M[5] = 0
## P3[2]*M[6]-P3[1]*M[7]+P3[0]*M[8] = 0
## P4[2]*M[9]-P4[1]*M[10]+P4[0]*M[11] = 0
## P5[2]*M[12]-P5[1]*M[13]+P5[0]*M[14] = 0
vectZero = vector(S, [0 for i in range(10)])
assert(P1[2]*M[0]-P1[1]*M[1]+P1[0]*M[2] == vectZero)
assert(P2[2]*M[3]-P2[1]*M[4]+P2[0]*M[5] == vectZero)
assert(P3[2]*M[6]-P3[1]*M[7]+P3[0]*M[8] == vectZero)
assert(P4[2]*M[9]-P4[1]*M[10]+P4[0]*M[11] == vectZero)
assert(P5[2]*M[12]-P5[1]*M[13]+P5[0]*M[14] == vectZero)


M1 = M.matrix_from_rows([0, 1, 3, 4, 6, 7, 9, 10, 12, 13])
## M1 is a rank 9 matrix:
assert(M1.det().is_zero())
m9 = M1.minors(9)
## there are non zero minors of order 9:
assert(Set(m9) != Set([S(0)]))

## Here we want to see if it is possible to have a configuration 
## of the points in which P1, ..., P5 are all distinct and
## in which the corresponding M1 has rank 8.

## The ideal generated by m9, after saturations, is (1):
id_all = S.ideal(m9).saturation(u2*v2*l2*w2)[0].\
saturation(S.ideal(matrix([P2, P3]).minors(2)))[0].\
saturation(S.ideal(matrix([P4, P5]).minors(2)))[0]
assert(id_all.radical() == S.ideal(v1*l1, u1*w1))

## the ideal (v1*l1, u1*w1) says that in our configuration
## the point P2 = Q2 and P4 = Q4 or P2 = Q2 and P5 = Q4 or...
## i.e. we have P1+P2 tangent to the isopropic conic in P2 or P3
## and P1+P4 tangent to the isopropic conic in P4 or P5.

## conclusion: it is not possible to have M1 (and hence M) of rank 8.


print("""CONCLUSION: 
If the points P1, P2, P3, P4, P5 are such that P1+P2 and P1+P4 
are tangent to the isotropic conic (but are not tangent in P2 or
P4 or P3 or P5) the matrix Phi(P1, ..., P5) has rank 9""")

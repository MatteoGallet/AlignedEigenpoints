{
 "cells": [
  {
   "cell_type": "markdown",
   "id": "932aa706-2bd1-4335-bbaa-2b372082f2ab",
   "metadata": {},
   "source": [
    "# Proposition "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7582fae6-8da5-4f50-9b28-235d32d3247b",
   "metadata": {},
   "source": [
    "We know that $\\delta_2 = 0$ in general gives that \n",
    "$$P_3 = (s_{14} s_{15} s_{22} - s_{12}^2 s_{45}) P_1 + s_{12} (s_{11} s_{45} - s_{14} s_{15}) P_2$$\n",
    "hence, for some values of $s_{ij}$, the point $P_3$ is not defined.\n",
    "Here we want to see when this happens.\n",
    "The conclusion is that $P_3$ is always uniquely defined, unless:\n",
    "1) the points of the $V$-configuration are such that $s_{12}=0$, $s_{14}=0$\n",
    "   (or $s_{13}=0$, $s_{15}=0$, or...). In this case the unique cubic is a \n",
    "   cubic in which the eigenpoints are in configuration (5).\n",
    "2) the points of the $V$-configuration satisfy the condition \n",
    "   $s_{12} = 0$, $s_{23} = 0$. In this case, the line $P_1 \\vee P_2$ of the $V$-configuration\n",
    "   is tangent in $P_2$ to the isotropic conic, so the matrix $\\Phi(P_1, P_2, P_3)$ has \n",
    "   rank $5$ and $P_5$ can be chosen in a free way on the line $P_1 \\vee P_4$ and the \n",
    "   matrix $\\Phi(P_1, P_2, P_3, P_4, P_5)$ has rank $9$, (unless $P_1 \\vee P_4$ is tangent\n",
    "   to the isotropic conic...)\n",
    "3) The points satisfy the conditions $\\sigma(P_1, P_2) = 0$ and \n",
    "   $\\sigma(P_1, P_4) = 0$. In this case the $V$-configuration is given \n",
    "   by a point $P_1$ not on the isotropic conic and the other points on the two tangents \n",
    "   to the isotropic conic through $P_1$. If no points of the $V$-configurations are on \n",
    "   the isotropic conic, the two tangent lines are lines of eigenpoints, if (say)\n",
    "   $P_2$ and $P_4$ are on the isotropic conic, the matrix $\\Phi(P_1, P_2, P_3, P_4)$ has rank $8$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 41,
   "id": "ef6af78b-3f2c-4a72-9cb9-10a97b64ea31",
   "metadata": {},
   "outputs": [],
   "source": [
    "load(\"basic_functions.sage\")"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "ca86c82e-1253-4fbe-84e7-bd74338fc08c",
   "metadata": {},
   "source": [
    "We define three points $P_1$, $P_2$, and $P_4$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 42,
   "id": "5bb6a540-1664-4278-9154-b462cb2a3456",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = vector(S, (A1, B1, C1))\n",
    "P2 = vector(S, (A2, B2, C2))\n",
    "P4 = vector(S, (A4, B4, C4))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fe305dda-f7f7-4799-a6dc-618b6b1b0522",
   "metadata": {},
   "source": [
    "We write $\\delta_2$ in terms of the quantities $s_{ij}$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 43,
   "id": "120e36ec-8984-4b3c-a12b-3786e30e833f",
   "metadata": {},
   "outputs": [],
   "source": [
    "d2s = s12*s13*s45 - s14*s15*s23  "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "2506e963-fd5f-4064-a5d1-d1c18d2fe8e6",
   "metadata": {},
   "source": [
    "In general, we have that 5 points in a V-configuration are eigenpoints iff \n",
    "$$P_3 = (s_{14} s_{15} s_{22} - s_{12}^2 s_{45}) P_1 + s_{12} (s_{11} s_{45} - s_{14} s_{15}) P_2$$ \n",
    "unless the two coefficients of $P_1$ and $P_2$ are $0$.\n",
    "Here we study this particular case."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 44,
   "id": "dcf688bc-1d86-4291-8432-0df3232065b9",
   "metadata": {},
   "outputs": [],
   "source": [
    "Js = S.ideal(s14*s15*s22-s12^2*s45, s11*s12*s45-s12*s14*s15, d2s)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8775c785-7560-46c6-93e3-4f44f54586da",
   "metadata": {},
   "source": [
    "We compute the primary decomposition of the radical of the ideal $J_s$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 45,
   "id": "5dfd6937-b93d-4529-a623-4a03263ce64c",
   "metadata": {},
   "outputs": [],
   "source": [
    "pd = Js.radical().primary_decomposition()"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "52927cd9-19f9-478b-b142-a2c78462929b",
   "metadata": {},
   "source": [
    "We get $6$ ideals:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 46,
   "id": "c293f520-639c-4805-b36a-a51e26796516",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(len(pd)==6)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "82c26752-3f41-4e4a-954f-56d132e10e7c",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## The ideal $(s_{12}, s_{14})$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "fb356fb1-be14-4af0-b1e9-17d6043a2067",
   "metadata": {},
   "source": [
    "The first ideal is generated by $s_{14}$ and $s_{12}$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 47,
   "id": "d61836da-7402-4982-9a39-46a358666dd5",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(S.ideal(s12, s14) in pd)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "5018b4c2-07b4-43f3-8ae8-c5d2cdeea385",
   "metadata": {},
   "source": [
    "If $P_1$, $P_2$, and $P_4$ are such that $s_{12} = 0$, $s_{14} = 0$, then, as vectors, $P_1$ is the cross product of $P_2$ and $P_4$."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 48,
   "id": "8b1eaa2b-3dcf-4b60-a234-9ac7e7a20a64",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = wedge_product(P2, P4)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 49,
   "id": "7854f54d-aa29-4eb2-b6fe-10f8227cff2d",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(scalar_product(P1, P2) == 0)\n",
    "assert(scalar_product(P1, P4) == 0)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "322a7c72-58a0-4484-985a-008c4f67b981",
   "metadata": {},
   "source": [
    "We construct $P_3$ and $P_5$ so that the $5$ points are in a $V$-configuration"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 50,
   "id": "eaf4f504-1c4a-406c-bb13-8d36b358bda1",
   "metadata": {},
   "outputs": [],
   "source": [
    "P3 = u1*P1+u2*P2\n",
    "P5 = v1*P1+v2*P4"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "62a3075f-aa1e-44f1-b148-70af281d3bb5",
   "metadata": {},
   "source": [
    "With this choice, we have $\\delta_2(P_1, P_2, P_3, P_4, P_5) = 0$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 51,
   "id": "38c09a5d-9e5a-44f2-9f3c-115510bffa7c",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(delta2(P1, P2, P3, P4, P5) == 0)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "302ffddb-c4f5-4036-b934-d91b3408cdf6",
   "metadata": {},
   "source": [
    "We see therefore that we can define $P_3$ and $P_5$ in an arbitrary way:\n",
    "for any choice $\\delta_2$ is zero."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "760d6603-9db0-4b70-9318-da9a37d8cd7c",
   "metadata": {},
   "source": [
    "From here the computations are exactly the computations in the file \n",
    "config5.sage (up to a permutation of the points) and we see that in \n",
    "this case the eigenpoints of the cubic are in configuration (5)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "59c5abb6-602f-48b5-85f1-d271911b42ca",
   "metadata": {},
   "source": [
    "CONCLUSION:\n",
    "If $s_{12} = s_{14} = 0$, then the $V$-configuration has $\\delta_2$ = 0 and $P_3$ and $P_5$ \n",
    "can be freely chosen on the line $P_1 \\vee P_2$ and $P_1 \\vee P_4$, respectively."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "44152eb2-d045-4178-90e1-b4cb130babe7",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## The ideal $(s_{12}, s_{22}, s_{23})$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 62,
   "id": "7079006c-d08b-4130-80f8-c5be5767a444",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(S.ideal(s12, s22, s23) in pd)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "eb4fb53f-8a37-401e-b268-710a2d96e4f1",
   "metadata": {},
   "source": [
    "An easy computation shows that if $s_{22} = 0$ and $s_{12} = 0$, then $s_{23} = 0$, \n",
    "(or if $s_{12} = 0$ and $s_{23} = $0, then $s_{22} = 0$),\n",
    "so the ideal $(s_{12}, s_{22}, s_{23})$ is generated by $s_{12}$ and $s_{22}$.\n",
    "\n",
    "In particular, the line $P_1 \\vee P_2$ is tangent to the isotropic conic in $P_2$, \n",
    "hence the matrix $\\Phi(P_1, P_2, P_3)$ has rank $5$.\n",
    "\n",
    "Therefore, for all $P_3$ and $P_5$ the rank \n",
    "of the matrix $\\Phi(P_1, P_2, P_3, P_4, P_5)$ is $\\leq 9$, thus we have a \n",
    "$V$-configuration which satisfies $\\delta_2(P_1, P_2, P_3, P_4, P_5) = 0$\n",
    "for any choice of the points $P_3$ and $P_5$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "12530d26-ca7b-41f0-841b-d6fe390859d6",
   "metadata": {},
   "source": [
    "CONCLUSION FOR THE CASE $s12$, $s22$:\n",
    "the $V$-configuration is of eigenpoints, for every choice of $P_3$ and $P_5$\n",
    "(if also $P_1 \\vee P_4$ is tangent to the isotropic conic, the two tangent lines are \n",
    "lines of eigenpoints, as claimed in the section on eigenpoints \n",
    "of positive dimension)."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "80395427-ebea-405a-9439-622a4f992d71",
   "metadata": {},
   "source": [
    "Here is an example: \n",
    "it shows that in case $s_{12} = s_{22} = 0$ we have 7 eigenpoints\n",
    "with (apparently) no particular specific properties."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 63,
   "id": "7cc3a812-d459-4838-a647-5330846170db",
   "metadata": {},
   "outputs": [],
   "source": [
    "Q1, Q2 = vector(S, (0, 0, 1)), vector(S, (1, ii, 0))\n",
    "Q3 = vector(S, (1, -ii, 0))\n",
    "rt1 = matrix([Q1, Q2, (x, y, z)]).det()\n",
    "rt2 = matrix([Q1, Q3, (x, y, z)]).det()\n",
    "\n",
    "p1 = 1*Q1\n",
    "p2 = 1*Q2\n",
    "p3 = 2*p1+5*p2\n",
    "p4 = vector(S, (3, 1, 5))\n",
    "p5 = p1+3*p4\n",
    "assert(condition_matrix([p1, p2, p3, p4, p5], S, standard=\"all\").rank() == 9)\n",
    "cb = cubic_from_matrix(condition_matrix([p1, p2, p3, p4, p5], S, standard=\"all\"))\n",
    "assert(cb.is_prime())"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "f1051392-54d7-4959-9c18-e1185efa20a3",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## The ideal $(s_{15}, s_{12})$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 59,
   "id": "773c121a-e946-41be-8496-0351a2547880",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(S.ideal(s15, s12) in pd)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "dd7c0308-9e20-4856-8827-f808bac87d3c",
   "metadata": {},
   "source": [
    "This case is analog to the case $(s_{12}, s_{14})$ by swapping $4$ and $5$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "594f7128-0807-492a-8249-c2c6d5c9540b",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## The ideal $(s_{45}, s_{14})$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 60,
   "id": "2a86d4ea-de23-4c58-8110-6c44b4282f60",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(S.ideal(s45, s14) in pd)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7704d24d-1ba2-4936-aeb0-88334557fad5",
   "metadata": {},
   "source": [
    "In this case we have $s_{44} = 0$, indeed\n",
    "$$ \\left\\langle P_4, v_1 P_1 + v_2 P_4 \\right\\rangle = 0 $$\n",
    "$$ v_1 \\left\\langle P_1, P_4 \\right\\rangle + v_2 \\left\\langle P_4, P_4 \\right\\rangle = 0 $$\n",
    "$$ 0 + v_2 \\left\\langle P_4, P_4 \\right\\rangle = 0 $$\n",
    "$$ \\left\\langle P_4, P_4 \\right\\rangle = 0 $$\n",
    "because $v_2 \\neq 0$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "41cb1037-26ac-4e2f-afb1-61548d6040b3",
   "metadata": {},
   "source": [
    "CONCLUSION: THIS CASE IS THE SAME AS THE CASE $(s_{12}, s_{22}, s_{23})$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "4e8cc844-43f5-4c24-8d35-396c9f096ed4",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## The ideal $(s_{45}, s_{15})$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9095a8cf-f9cb-4e59-b2e9-08e34f34a7aa",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(S.ideal(s45, s15) in pd)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "403c0360-c0be-4d54-8dc8-8e9e78eadbbd",
   "metadata": {},
   "source": [
    "In this case we have $s_{55} = 0$ as in $(s_{45}, s_{14})$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "d4b0910e-5c2e-4f03-876d-1328f391d7b1",
   "metadata": {},
   "source": [
    "CONCLUSION: THIS CASE IS THE SAME AS THE CASE $(s_{12}, s_{22}, s_{23})$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "594fc7e3-3190-4b78-9225-2faf4de6f5b1",
   "metadata": {
    "jp-MarkdownHeadingCollapsed": true
   },
   "source": [
    "## The ideal $(s_{13}s_{22} - s_{12}s_{23}, s_{14}s_{15} - s_{11}s_{45}, s_{12}s_{13} - s_{11}s_{23}, s_{12}^2 - s_{11}s_{22})$"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 64,
   "id": "6e4f3bb5-3c8f-47ea-bd1f-28cf42c42b75",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(S.ideal(\n",
    "    [\n",
    "        s13*s22 - s12*s23, \n",
    "        s14*s15 - s11*s45,\n",
    "        s12*s13 - s11*s23, \n",
    "        s12^2 - s11*s22\n",
    "    ]\n",
    ") in pd)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "7cf1428d-bbb4-4110-bb76-4e2cc57259da",
   "metadata": {},
   "source": [
    "In this case we determine the ideal in the variables $A_1, \\dotsc, C_4$,\n",
    "but first we have to re-define the points."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 65,
   "id": "0c9817bb-5b72-4120-8a35-085ff725bada",
   "metadata": {},
   "outputs": [],
   "source": [
    "P1 = vector(S, (A1, B1, C1))\n",
    "P2 = vector(S, (A2, B2, C2))\n",
    "P4 = vector(S, (A4, B4, C4))\n",
    "P3 = u1*P1+u2*P2\n",
    "P5 = v1*P1+v2*P4"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 66,
   "id": "03e4ddd3-19b6-4bff-bf4b-2de5c2081651",
   "metadata": {},
   "outputs": [],
   "source": [
    "I = S.ideal(\n",
    "    [\n",
    "        s13*s22 - s12*s23, \n",
    "        s14*s15 - s11*s45,\n",
    "        s12*s13 - s11*s23, \n",
    "        s12^2 - s11*s22\n",
    "    ]\n",
    ")\n",
    "J = I.subs(\n",
    "    {\n",
    "        s11: scalar_product(P1, P1),\n",
    "        s22: scalar_product(P2, P2),\n",
    "        s13: scalar_product(P1, P3),\n",
    "        s12: scalar_product(P1, P2),\n",
    "        s23: scalar_product(P2, P3),\n",
    "        s14: scalar_product(P1, P4),\n",
    "        s15: scalar_product(P1, P5),\n",
    "        s45: scalar_product(P4, P5)\n",
    "    }\n",
    ") "
   ]
  },
  {
   "cell_type": "markdown",
   "id": "230fc551-793d-4a43-b813-21da977c75be",
   "metadata": {},
   "source": [
    "We saturate $J$ w.r.t. the condition that $v_2 \\neq 0$ and \n",
    "w.r.t. the condition that $P_1$, $P_2$, and $P_4$ are not aligned."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 68,
   "id": "cc3a4de9-030b-40c1-820b-8aca0d70cc6d",
   "metadata": {},
   "outputs": [],
   "source": [
    "J = J.saturation(v2)[0]\n",
    "J = J.saturation(matrix([P1, P2, P4]).det())[0]"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "001c7999-bd86-46a0-a574-b5cdcf32a44b",
   "metadata": {},
   "source": [
    "We study the ideal $J$"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "a2876134-dec0-4f39-8148-af15b272f099",
   "metadata": {},
   "source": [
    "$J$ contains the two polynomials $\\sigma(P_1, P_2)$ and $\\sigma(P_1, P_4)$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 69,
   "id": "fc526839-fcaa-4d11-8097-2eefab23f38d",
   "metadata": {},
   "outputs": [],
   "source": [
    "assert(sigma(P1, P2) in J)\n",
    "assert(sigma(P1, P4) in J)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "def8a7b9-334a-426c-9730-1f68d8207d76",
   "metadata": {},
   "source": [
    "Moreover, $J$ and the ideal $\\bigl(\\sigma(P_1, P_2), \\sigma(P_1, P_4)\\bigr)$\n",
    "coincide if $P_1$, $P_2$, and $P_4$ are not aligned:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 70,
   "id": "ee674be1-6dda-415a-bd45-b11dcd66078c",
   "metadata": {},
   "outputs": [],
   "source": [
    "I = S.ideal(sigma(P1, P2), sigma(P1, P4)).saturation(matrix([P1, P2, P4]).det())[0]\n",
    "assert(J == I)"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "bc8cc3ee-b391-4866-bde5-dc92a0cf3e5d",
   "metadata": {},
   "source": [
    "Hence, in this case the $V$-configuration is obtained by a point $P_1$ and the two tangents to \n",
    "the isotropic conic passing through $P_1$. \n",
    "\n",
    "In general, the $V$-configuration is such that the two lines are lines of eigenpoints and the cubic splits \n",
    "into the conic $(x^2 + y^2 + 2/3*z^2)$ and a line.\n",
    "This case is discussed in the section on eigenpoints of positive dimension."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "9c01c855-7c28-4ae5-9316-b0af78ec0f96",
   "metadata": {},
   "source": [
    "Here is an example:\n",
    "example of $P_1 \\vee P_2$ tangent to the isotropic conic in $P_2$:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 73,
   "id": "74dd6d31-b74b-49be-8642-6d6334029fa8",
   "metadata": {},
   "outputs": [],
   "source": [
    "Q1, Q2, Q3 = vector(S, (0, 0, 1)), vector(S, (1, ii, 0)), vector(S, (1, -ii, 0))\n",
    "rt1 = matrix([Q1, Q2, (x, y, z)]).det()\n",
    "rt2 = matrix([Q1, Q3, (x, y, z)]).det()\n",
    "\n",
    "p1 = 1*Q1\n",
    "p2 = Q1+3*Q2\n",
    "p3 = 2*Q1+5*Q2\n",
    "p4 = Q1+7*Q3\n",
    "p5 = Q1-2*Q3\n",
    "\n",
    "M = condition_matrix([p1, p2, p3, p4, p5], S, standard=\"all\")\n",
    "assert(M.rank() == 9)\n",
    "cb = cubic_from_matrix(M)\n",
    "assert(cb == z*(x^2 + y^2 + 2/3*z^2))"
   ]
  },
  {
   "cell_type": "markdown",
   "id": "8db6a4d8-8c96-46d3-9fa3-6032545fe655",
   "metadata": {},
   "source": [
    "In the particular case in which $P_2$ and $P_4$ are on the isotropic conic,\n",
    "the matrix $\\Phi(P_1, P_2, P_3, P_4, P_5)$ has rank $8$ and \n",
    "a generic cubic of the corresponding pencil of cubics is smooth\n",
    "and has $7$ eigenpoints. \n",
    "The case is studied in the section on $V$-configurations of rank $8$."
   ]
  },
  {
   "cell_type": "markdown",
   "id": "1e53f85f-f5e0-4f1a-a49c-112644543128",
   "metadata": {},
   "source": [
    "Here is an example:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": 75,
   "id": "72d034a1-0755-4628-9d4a-8f9d50a644ac",
   "metadata": {},
   "outputs": [],
   "source": [
    "Q1, Q2, Q3 = vector(S, (0, 0, 1)), vector(S, (1, ii, 0)), vector(S, (1, -ii, 0))\n",
    "rt1 = matrix([Q1, Q2, (x, y, z)]).det()\n",
    "rt2 = matrix([Q1, Q3, (x, y, z)]).det()\n",
    "\n",
    "p1 = 1*Q1\n",
    "p2 = 1*Q2\n",
    "p3 = 2*Q1+5*Q2\n",
    "p4 = 1*Q3\n",
    "p5 = Q1-2*Q3\n",
    "\n",
    "M = condition_matrix([p1, p2, p3, p4, p5], S, standard=\"all\")\n",
    "assert(M.rank() == 8)\n",
    "M1 = M.matrix_from_rows([1, 2, 3, 4, 7, 9, 10, 13])\n",
    "assert(M1.rank() == 8)\n",
    "M2 = M1.stack(vector(S, (1, 3, 6, -4, 0, 3, 2, 1, -5, 7)))\n",
    "M2 = M2.stack(vector(S, mon))\n",
    "cb = M2.det()"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "SageMath 10.1",
   "language": "sage",
   "name": "sagemath"
  },
  "language_info": {
   "codemirror_mode": {
    "name": "ipython",
    "version": 3
   },
   "file_extension": ".py",
   "mimetype": "text/x-python",
   "name": "python",
   "nbconvert_exporter": "python",
   "pygments_lexer": "ipython3",
   "version": "3.10.6"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
